DEFINE
    VALUE Ltime 0
    ARRAY size [160 80]
    #ARRAY size [640 320]
    ARRAY Lresult 
    ARRAY Ltemp
    ARRAY newF

    DRAW

    FUNCTION makeNewF
        "F" AS [newF]
        "+" AS [newF]
        "F" AS [newF]
        "-" AS [newF]
        "F" AS [newF]
        "-" AS [newF]
        "F" AS [newF]
        "+" AS [newF]
        "F" AS [newF]
    END

    FUNCTION Koch
        WITH Lresult EACH
            . MATCH
                ON "F" DO
                    WITH newF COPY Ltemp
                END
                ON "+" DO
                    "+" AS [Ltemp]
                END
                ON "-" DO
                    "-" AS [Ltemp]
                END
            END
        END
        ARRAY Lresult
        WITH Ltemp COPY Lresult
        ARRAY Ltemp
    END
    

    THING turtle
        INIT
            0 AS THIS x
            0 AS THIS y 
            90 AS THIS angle
        END
        THIS goto
            . AS THIS y
            . AS THIS x 
            THIS x THIS y DRAW
        END
        THIS right
            THIS angle + 360 % AS THIS angle
        END
        THIS move
            THIS angle 22 + 45 / MATCH
                ON 0 DO
                    1 THIS y + 2 [size] % AS THIS y 
                    THIS x THIS y DRAW
                END
                ON 1 DO
                    1 THIS x + 1 [size] % AS THIS x 
                    1 THIS y + 2 [size] % AS THIS y 
                    THIS x THIS y DRAW
                END
                ON 2 DO
                    1 THIS x + 1 [size] % AS THIS x
                    THIS x THIS y DRAW
                END
                ON 3 DO
                    1 THIS x + 1 [size] % AS THIS x
                    2 [size] THIS y + 1 - 2 [size] % AS THIS y
                    THIS x THIS y DRAW
                END
                ON 4 DO
                    2 [size] THIS y + 1 - 2 [size] % AS THIS y
                    THIS x THIS y DRAW
                END
                ON 5 DO
                    1 [size] THIS x + 1 - 1 [size] % AS THIS x 
                    2 [size] THIS y + 1 - 2 [size] % AS THIS y
                    THIS x THIS y DRAW
                END
                ON 6 DO
                    1 [size] THIS x + 1 - 1 [size] % AS THIS x 
                    THIS x THIS y DRAW
                END
                ON 7 DO
                    1 [size] THIS x + 1 - 1 [size] % AS THIS x 
                    1 THIS y + 2 [size] % AS THIS y
                END
            END
        END
        THIS forward
            . AS steps
            0 AS s
            {s steps !=} DO
                turtle move
                s 1 + AS s
            END
        END
    END
END


ARRAY Lresult
"F" AS [Lresult]

DRAW NEW
#0 DRAW RATE 

0 0 DRAW 
1 [size] 2 [size] DRAW
0 0 turtle goto

newF 0 == DO
    makeNewF
END 

0 AS n
INPUT AS nn 

{n nn !=} DO
    Koch
    n 1 + AS n
END

Lresult PRINT   


WITH Lresult EACH
    . MATCH
        ON "F" DO
            4 turtle forward
        END
        ON "+" DO
            270 turtle right
        END
        ON "-" DO
            90 turtle right
        END
    END

END
DRAW NOW



